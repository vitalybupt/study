plugins {
    id 'java'
    id 'application'
}

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    implementation "io.vertx:vertx-core:${rootProject.versions.vertx}"
    implementation "io.vertx:vertx-service-proxy:${rootProject.versions.vertx}"
    implementation "io.vertx:vertx-jdbc-client:${rootProject.versions.vertx}"
    implementation "io.vertx:vertx-rx-java2:${rootProject.versions.vertx}"
    implementation "com.github.rjeschke:txtmark:0.13"
    implementation "io.vertx:vertx-auth-jdbc:${rootProject.versions.vertx}"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    annotationProcessor "io.vertx:vertx-service-proxy:${rootProject.versions.vertx}"
    annotationProcessor "io.vertx:vertx-lang-js-gen:${rootProject.versions.vertx}"
    annotationProcessor "io.vertx:vertx-rx-java2-gen:${rootProject.versions.vertx}" 
    annotationProcessor "io.vertx:vertx-codegen:${rootProject.versions.vertx}"
    runtime "org.hsqldb:hsqldb:2.3.4"
    runtime "io.vertx:vertx-web:${rootProject.versions.vertx}"
}

mainClassName = 'io.vitaly.wiki.Launcher'
def mainVerticleName = 'io.vitaly.vertx.MainVerticle'

// Vert.x watches for file changes in all subdirectories
// of src/ but only for files with .java extension
def watchForChange = 'src/**/*.java'

// Vert.x will call this task on changes
def doOnChange = "..${File.separator}gradlew build"

jar {
    manifest {
        attributes(
                "Main-Class": mainClassName,
                "Main-Verticle": mainVerticleName
        )
    }
}

task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
    source = sourceSets.main.java
    description 'Generates Vertx js and rxjava shims'
    options.annotationProcessorPath = configurations.annotationProcessor
    classpath = configurations.compileClasspath
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
	"-proc:only",
	"-processor", "io.vertx.codegen.CodeGenProcessor",
	"-Acodegen.output=${project.projectDir}/src/main"
    ]
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8
    
    dependsOn annotationProcessing
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

runShadow {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

sourceSets {
  main {
	java {
	    srcDirs += 'src/main/generated'
	}
    }
}
